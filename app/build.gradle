apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.appdistribution'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.skichrome.oc.easyvgp"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        //resConfigs "en, fr"
        multiDexEnabled true
    }

    signingConfigs {
        release {
            def keystore = rootProject.file("keystore.properties")
            if (keystore.exists()) {
                def keyProp = new Properties()
                keyProp.load(new FileInputStream(keystore))
                storeFile file(keyProp["STORE_FILE"])
                storePassword keyProp["STORE_PASS"]
                keyAlias keyProp["KEY_ALIAS"]
                keyPassword keyProp["KEY_PASS"]
            } else {
                storeFile file(System.getenv("STORE_FILE"))
                storePassword System.getenv("STORE_PASS")
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASS")
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            firebaseAppDistribution {
                serviceCredentialsFile = System.getenv("FIREBASE_APP_DISTRIBUTION_FILE")
                releaseNotesFile "app/release_notes.md"
                testers = "campeoltoni@gmail.com"
                apkPath "app/build/outputs/apk/demo/release/app-demo-release.apk"
            }
        }
    }

    flavorDimensions "versions"
    productFlavors {
        dev {
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
        }
        demo {
            applicationIdSuffix ".demo"
            versionNameSuffix "-demo"
        }
        production {
        }
    }

    dataBinding {
        enabled = true
        enabledForTests = true
    }

    viewBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    testOptions.unitTests {
        includeAndroidResources = true
        returnDefaultValues = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:multidex:1.0.3'
    // Kotlin support
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.2.0'

    // Dependencies for local unit tests
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.hamcrest:hamcrest-all:$hamcrestVersion"
    testImplementation "androidx.arch.core:core-testing:$archTestingVersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"

    // Dependencies for Android instrumented unit tests
    androidTestImplementation "junit:junit:$junitVersion"
    androidTestImplementation "org.mockito:mockito-core:$mockitoVersion"
    androidTestImplementation "com.linkedin.dexmaker:dexmaker-mockito:$dexMakerVersion"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"

    debugImplementation "androidx.fragment:fragment-testing:$fragmentVersion"
    debugImplementation "androidx.test:core:$androidXTestCoreVersion"

    // AndroidX Test - JVM testing
    testImplementation "androidx.test:core-ktx:$androidXTestCoreVersion"
    testImplementation "androidx.test.ext:junit:$androidXTestExtKotlinRunnerVersion"
    androidTestImplementation "androidx.test.ext:junit-ktx:$androidXTestExtKotlinRunnerVersion"

    // AndroidX Test - Instrumented testing
    androidTestImplementation "androidx.test.ext:junit:$androidXTestExtKotlinRunnerVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"

    // UI Libraries
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.preference:preference:1.1.0'

    // Glide image loader
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'

    // Navigation Component
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"

    // Design library
    implementation 'com.google.android.material:material:1.2.0-alpha05'

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.fragment:fragment-ktx:$fragmentKtxVersion"
    kapt "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    // Database
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // Firebase dependencies
    implementation 'com.google.firebase:firebase-core:17.3.0'
    implementation 'com.google.firebase:firebase-firestore-ktx:21.4.2'
    implementation 'com.firebaseui:firebase-ui-auth:6.2.0'
    implementation 'com.google.firebase:firebase-storage-ktx:19.1.1'

    demoImplementation 'com.google.firebase:firebase-crashlytics:17.0.0-beta03'
    demoImplementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    demoImplementation 'com.google.firebase:firebase-perf:19.0.5'

    productionImplementation 'com.google.firebase:firebase-crashlytics:17.0.0-beta03'
    productionImplementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    productionImplementation 'com.google.firebase:firebase-perf:19.0.5'
}
